#% text_encoding = iso8859_1

def_slotted_exemplar(
	:sonar_test_suite,
	{
	},
	{@user:sonar_test_base})
$

_method sonar_test_suite.new(p_name, p_parent)
	_return _clone.init(p_name, p_parent)
_endmethod
$

_method sonar_test_suite.write_to_stream(p_xml_stream)
	# output by file
	_local l_children_per_file << equality_hash_table.new()
	_for i_child _over .children.fast_elements()
	_loop
		_local l_test_cases << l_children_per_file.at_or_new_if_absent(i_child.source_file, rope)
		l_test_cases.add(i_child)
	_endloop
	
	_for i_path, i_children _over l_children_per_file.fast_keys_and_elements()
	_loop
		p_xml_stream.start_element("file", :path, system.canonicalise(i_path))
		_for i_child _over i_children.fast_elements()
		_loop
			i_child.write_to_stream(p_xml_stream)
		_endloop
		p_xml_stream.end_element("file")
	_endloop
_endmethod
$

_method sonar_test_suite.show_on(p_stream)
	p_stream.write(_self.class_name, %(, .name, ", ", .parent, %))
_endmethod
$
